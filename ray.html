<!-- Copyright 2010 Robert Scott Dionne. All Rights Reserved. -->
<html>
  <head>
    <script src="base.js" type="application/javascript"></script>
    <script id="v" type="x-shader/x-vertex">
      uniform vec3 translate;
      attribute vec3 position;
      varying vec3 ray;
      void main() {
        mat4 t = mat4(1.0);
        t[3][0] = translate.x;
        t[3][1] = translate.y;
        t[3][2] = translate.z;
        mat4 p = mat4(1.0);
        p[2][2] = -1001.0 / 999.0;
        p[2][3] = -1.0;
        p[3][2] = -2000.0 / 999.0;
        p[3][3] = 0.0;
        vec4 d = /*t */ vec4(position, 1);
        ray = d.xyz;
        gl_Position = p * d;
      }
    </script>
    <script id="f" type="x-shader/x-fragment">
      uniform bool debug;
      uniform int distanceFn;
      uniform lowp vec3 translate;
      varying lowp vec3 ray;

      lowp float distance(lowp vec3 pos);
      lowp vec3 rotateY(lowp vec3 pos, lowp float a);
      lowp vec3 rotate(lowp vec3 pos, lowp float theta, lowp vec3 u);

      lowp float r = 0.22;
      lowp float R = 0.5;
      void main() {
        lowp vec3 pos = vec3(0);
        lowp vec3 dir = normalize(ray);
        lowp float eps = 0.002;//1.001 - dot(dir, vec3(0.0, 0.0, -1.0));
        bool hit = false;
        highp float count = 0.0;
        lowp vec3 p;
        lowp vec3 cr = cross(vec3(0.0, 0.0, -1.0), dir);
            //normalize(vec3(-0.5, 0.0, -4.0)));
        lowp vec3 n = normalize(cr);
        highp float th = asin(length(cr));
        for (int i = 0; i < 1000; ++i) {
          lowp vec3 q = pos;
          lowp float a = 1000.0;
          q.x /= a;
          q.y /= a;
          p = rotate(q, th, n)-translate;
          lowp float dist = distance(p);
          if (length(pos) > 100.0) {
            break;
          }
          if (dist < max(0.01 * pos.z, eps/2.0)) {
            hit = true;
            break;
          }
          count += 0.02;
          pos += 0.9 * dir * dist;
        }
        lowp float red = 0.;//1.0-clamp(length(pos) / 100.0, 0.0, 1.0);
        lowp float blue = count;
        if (hit) {
          lowp vec3 dx = vec3(0.01, 0.0,  0.0);
          lowp vec3 dy = vec3(0.0,  0.01, 0.0);
          lowp vec3 dz = vec3(0.0,  0.0,  0.01);
          lowp vec3 normal = normalize(vec3(
            distance(p+dx) - distance(p-dx),
            distance(p+dy) - distance(p-dy),
            distance(p+dz) - distance(p-dz)));
          lowp vec3 lightdir = pos - vec3(0, 0, -2);
          lowp vec3 nlightdir = normalize(lightdir);
          lowp vec3 lightdir2 = pos - vec3(4, 0, -5);
          lowp vec3 nlightdir2 = normalize(lightdir2);
          lowp vec3 lightdir3 = pos - vec3(-4, 0, -5);
          lowp vec3 nlightdir3 = normalize(lightdir3);
          lowp float intensity = clamp(-dot(nlightdir, normal), 0.0, 1.0);
          lowp float intensity2 = clamp(-dot(nlightdir2, normal)/
              length(lightdir2), 0.0, 1.0);
          lowp float intensity3 = clamp(-dot(nlightdir3, normal)/
              length(lightdir3), 0.0, 1.0);
          lowp float spec =pow(clamp(-dot(normalize(dir),
              reflect(nlightdir, normal)), 0.0, 1.0), 80.0);
          lowp float spec2 =pow(clamp(-dot(normalize(dir),
              reflect(nlightdir2, normal)), 0.0, 1.0), 80.0);
          lowp float spec3 =pow(clamp(-dot(normalize(dir),
              reflect(nlightdir3, normal)), 0.0, 1.0), 80.0);
          if (debug) {
            gl_FragColor = vec4(0., count, 0., 1.);
          } else {
            gl_FragColor = vec4(intensity +intensity3+ spec+spec3,
                intensity +spec, intensity +intensity2+ spec+spec2, 1.0);
          }
        } else {
          if (debug) {
            gl_FragColor = vec4(0., count, 0., 1.);
          } else {
           discard;
         }
        }
      }

      lowp float sphere(lowp vec3 pos);
      lowp float leaf(lowp vec3 pos);
      lowp float torus(lowp vec3 pos);
      lowp float cube(lowp vec3 pos);
      lowp float twistedCube(lowp vec3 pos);
      lowp float column(lowp vec3 pos);
      lowp float columnTwo(lowp vec3 pos);

      lowp float rectangularPrism(lowp vec3 pos, lowp vec3 pos);
      lowp float twistedRectangularPrism(lowp vec3 pos, lowp vec3 pos);
      lowp vec3 scale(lowp vec3 pos);

      lowp float both(lowp vec3 pos) {
        return min(sphere(pos-vec3(1.1, 0.0, 0.0)), cube(pos+vec3(1.1, 0.0, 0.0)));
      }
      
      lowp float distance(lowp vec3 pos) {
        if (distanceFn == 0) {
          return sphere(pos);
        } else if (distanceFn == 1) {
          return leaf(pos);
        } else if (distanceFn == 2) {
          return torus(pos);
        } else if (distanceFn == 3) {
          return cube(pos);
        } else if (distanceFn == 4) {
          return twistedCube(pos);
        } else if (distanceFn == 5) {
          return column(pos);
        } else {
          return columnTwo(pos);
        }
      }

      lowp float cube(lowp vec3 pos) {
        return rectangularPrism(pos, vec3(1.0));
      }

      lowp float twistedCube(lowp vec3 pos) {
        return twistedRectangularPrism(pos, vec3(1.0));
      }

      lowp float rectangularPrism(lowp vec3 pos, lowp vec3 size) {
        lowp vec3 dist = abs(pos) - size;
        return min(max(max(dist.x, dist.y), dist.z),
            length(max(dist, 0.0)));
      }

      lowp float twistedRectangularPrism(lowp vec3 pos, lowp vec3 size) {
        pos = rotateY(pos, pos.y);
        return rectangularPrism(pos, size);
      }

      lowp float sphere(lowp vec3 pos) {
        return length(pos)-sqrt(2.0);
      }

      lowp float torus(lowp vec3 pos) {
        return length(vec2(length(pos.xz)-R,pos.y))-r;
      }

      lowp float column(lowp vec3 pos) {
        lowp float distTop = dot(pos, vec3(0.0, 1.0, 0.0))-2.0;
        lowp float distBottom = dot(pos, vec3(0.0, -1.0, 0.0))-2.0;
        lowp float theta = atan(pos.z, pos.x);
        lowp float distCyl = length(pos.xz)-0.25;
        distCyl -= 1.0/16.0-(pos.y/8.0)*(pos.y/8.0);
        lowp float a = ((mod(12.0*theta/3.14159+0.5,1.0))-0.5);
        distCyl -= 0.05*a*a;
        return max(max(distTop, distBottom), distCyl);
      }

      lowp float columnTwo(lowp vec3 pos) {
        return twistedRectangularPrism(pos, vec3(1.0, 4.0, 1.0));
      }

      lowp float leaf(lowp vec3 pos) {
        lowp vec3 p = pos;
        p.z += sin(p.y / 8.0);
        return max(max(dot(p, vec3(0.0, 0.0, 1.0)), length(p.xy*vec2(1.0,1.5))-1.0),
            dot(p, vec3(0.0, 0.0, -1.0))-0.01);
      }

      lowp vec3 scale(lowp vec3 pos) {
        lowp mat3 s = mat3(1.0);
        s[0][0] = 0.5;
        return s * pos;
      }

      lowp vec3 rotate(lowp vec3 pos, lowp float theta, lowp vec3 u) {
        lowp mat3 r = mat3(0.0);
        r[0][0] = cos(theta)+u.x*u.x*(1.0-cos(theta));
        r[0][1] = u.y*u.x*(1.0-cos(theta))+u.z*sin(theta);
        r[0][2] = u.z*u.x*(1.0-cos(theta))-u.y*sin(theta);
        r[1][0] = u.x*u.y*(1.0-cos(theta))-u.z*sin(theta);
        r[1][1] = cos(theta)+u.y*u.y*(1.0-cos(theta));
        r[1][2] = u.z*u.y*(1.0-cos(theta))+u.x*sin(theta);
        r[2][0] = u.x*u.z*(1.0-cos(theta))+u.y*sin(theta);
        r[2][1] = u.y*u.z*(1.0-cos(theta))-u.x*sin(theta);
        r[2][2] = cos(theta)+u.z*u.z*(1.0-cos(theta));
        return r * pos;
      }

      lowp vec3 rotateY(lowp vec3 pos, lowp float a) {
        lowp mat3 r = mat3(1);
        r[0][0] = cos(a);
        r[2][0] = sin(a);
        r[0][2] = -sin(a);
        r[2][2] = cos(a);
        return r * pos;
      }
    </script>
  </head>
  <body onload="ray.load();">
    <div style="float: left;">
      <canvas id="c"></canvas>
    </div>
    <p><b>Controls</b></p>
    <table>
      <tr><th>Shape Translation</th></tr>
      <tr><td><b>d</b></td><td>Positive X.</td></tr>
      <tr><td><b>a</b></td><td>Negative X.</td></tr>
      <tr><td><b>w</b></td><td>Positive Y.</td></tr>
      <tr><td><b>s</b></td><td>Negative Y.</td></tr>
      <tr><td><b>z</b></td><td>Positive Z.</td></tr>
      <tr><td><b>q</b></td><td>Negative Z.</td></tr>
      <tr><th>Shape Selection</th></tr>
      <tr><td><b>n</b></td><td>Next shape.</td></tr>
      <tr><td><b>p</b></td><td>Previous shape.</td></tr>
      <tr><th>Debug Mode (loop iterations per fragment)</th></tr>
      <tr><td><b>y</b></td><td>Toggle.</td></tr>
    </table>
  </body>
</html>
